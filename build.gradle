plugins {
    id 'com.moowork.node' version '0.10'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

version = '1.0.0-SNAPSHOT'
group = 'de.undercouch'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

configurations {
    core
    codegen
}

dependencies {
    compile 'io.vertx:vertx-core:3.0.0-milestone6'
    compile 'io.vertx:vertx-lang-js:3.0.0-milestone6'

    testCompile 'junit:junit:4.12'

    core group: 'io.vertx', name: 'vertx-core', version: '3.0.0-milestone6', classifier: 'sources'

    codegen 'io.vertx:vertx-core:3.0.0-milestone6'
    codegen 'io.vertx:vertx-codegen:3.0.0-milestone6'
    codegen 'io.vertx:vertx-docgen:3.0.0-milestone6'
    codegen 'log4j:log4j:1.2.16'
    codegen 'org.slf4j:slf4j-api:1.6.2'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

// package javadoc into a jar file
task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// package source into a jar file
task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

// define artifacts for upload
artifacts {
    archives jar
    archives packageJavadoc
    archives packageSources
}

// sign all artifacts
signing {
    required { gradle.taskGraph.hasTask(uploadArchives) }
    sign configurations.archives
}

// remove test dependencies from configuration-to-scope mapping
// this also removes them from the maven pom file
conf2ScopeMappings.mappings.remove(configurations.testCompile)

uploadArchives {
    repositories {
        mavenDeployer {
            // sign artifacts before upload
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            
            // upload to sonatype OSS
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: this.hasProperty('sonatypeUsername') ? sonatypeUsername : '',
                    password: this.hasProperty('sonatypePassword') ? sonatypePassword : '')
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: this.hasProperty('sonatypeUsername') ? sonatypeUsername : '',
                    password: this.hasProperty('sonatypePassword') ? sonatypePassword : '')
            }
            
            // pom file details
            pom.project {
               name 'vertx-lang-typescript'
               packaging 'jar'
               description 'TypeScript support for Vert.x 3.0'
               url 'https://github.com/michel-kraemer/vertx-lang-typescript'
 
               scm {
                   url 'https://github.com/michel-kraemer/vertx-lang-typescript'
                   connection 'scm:git:git://github.com/michel-kraemer/vertx-lang-typescript.git'
                   developerConnection 'scm:git:ssh:git@github.com:michel-kraemer/vertx-lang-typescript.git'
               }
 
               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }
 
               developers {
                   developer {
                       id 'michel-kraemer'
                       name 'Michel Kraemer'
                       email 'michel@undercouch.de'
                   }
               }
           }
        }
    }
}

node {
  version = '0.12.4'
  npmVersion = '2.1.6'
  download = true
}

sourceSets {
    main {
        resources {
            srcDir 'src-gen/main/resources'
        }
    }
}

task copyCore(type: Copy) {
    from {
        configurations.core.collect { zipTree(it) }
    }
    into new File(buildDir, 'core')
    include "io/vertx/core/**"
}

task generateTypeDefinitions(type: JavaCompile, dependsOn: copyCore) {
    source = new File(buildDir, 'core')
    classpath = configurations.codegen + files(sourceSets.main.resources.srcDirs)
    options.compilerArgs = [
            '-proc:only',
            '-processor', 'io.vertx.codegen.CodeGenProcessor',
            '-AoutputDirectory=' + new File(buildDir, 'generated'),
    ]
    destinationDir = new File(buildDir, 'generated')
}

task copyTypeDefinitions(type: Copy, dependsOn: generateTypeDefinitions) {
    from new File(generateTypeDefinitions.destinationDir, 'resources/vertx-typescript')
    into new File(projectDir, 'src-gen/main/resources/vertx-typescript')
}

task copyRequiredResources(type: Copy, dependsOn: [copyTypeDefinitions, npmInstall]) {
    from file('node_modules/typescript')
    into 'src-gen/main/resources/typescript'
}

processResources.dependsOn(copyRequiredResources)

eclipseClasspath.dependsOn(processResources)
